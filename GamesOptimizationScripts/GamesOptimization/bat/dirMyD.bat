:: **********************************************************************
::dirMyd
:: Generates a list of directories inside a given folder with creation
:: time stamps YYYYMMDD at the beginning of each line. List is stored to
:: temporal file. With "scr" command file will be sorted and transformed
:: to usable MyDefrag script.
::
:: Created for Games Optimization Scripts
:: By Rohk, 2013
:: Tested in Windows XP, 7 and 8
::
:: Feel free to use in your own scripts.
:: **********************************************************************
@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION

:: Settings for Games Optimization Script ::
SET VARIABLENAME=Game
SET ZONEPATH=zone\Game
SET FILENAME=GameDirectories
SET NOTALLOWED=Steam,Desura,Origin

:: Parameters ::
IF /I "%1"=="scr" CALL:CreateScript & GOTO:End
IF "%1"=="" GOTO:Syntax
IF /I "%1"=="/S" (
  FOR /F "tokens=2,*" %%A IN ("%*") DO (
    SET DRIVE=%%A
    SET FOLDER=%%B
  )
) ELSE (
  SET FOLDER=%*
)
IF DEFINED DRIVE IF NOT "%FOLDER:~1,2%"==":\" CALL:SearchDirectory "%FOLDER%"
IF NOT EXIST "%FOLDER%" ECHO Folder not found^^! & GOTO:EOF

:: Temporal direcotry list creation ::
CD /D %~DP0
ECHO Folder Found^^!
REM Delims= is a TAB followed by a space in the next two lines.
FOR /F "SKIP=2 TOKENS=2* DELIMS=	 " %%A IN ('REG QUERY "HKCU\Control Panel\International" /v iDate') DO SET _iDate=%%B
FOR /F "SKIP=2 TOKENS=2* DELIMS=	 " %%A IN ('REG QUERY "HKCU\Control Panel\International" /v sDate') DO SET _sDate=%%B
REM Set folder tokens starting position according to time regional settings
SET _tok=5
FOR /F "USEBACKQ SKIP=4 TOKENS=3 DELIMS= " %%A IN (`DIR /A:D/O:D/T:C "%FOLDER%"`) DO (
    IF NOT "%%A"=="AM" IF NOT "%%A"=="PM" SET _tok=4
    goto :break
)
:break
(
FOR /F "USEBACKQ SKIP=4 TOKENS=1,%_tok%* DELIMS= " %%A IN (`DIR /A:D/O:D/T:C "%FOLDER%"`) DO (
  SET _fdate=
  CALL:CheckIllegal %%B > nul
  IF NOT "%%B"=="." IF NOT "%%B"==".." IF ERRORLEVEL 1 CALL:GetDate %%A %_sDate%
  IF NOT "!_fdate!"=="" (IF NOT "%%C"=="" (ECHO !_fdate!*!FOLDER!\%%B %%C) ELSE (ECHO !_fdate!*!FOLDER!\%%B))
))>>%FILENAME%.tmp
:End
ECHO done.
EXIT /B

:CheckIllegal
FOR %%A IN (%NOTALLOWED%) DO (
  ECHO "%1 " | FINDSTR /I /B /R /C:"\"%%A "
  IF ERRORLEVEL 0 GOTO:EOF
)
GOTO:EOF

:: Method created by TheOutcaste from techguy forums:
:: http://forums.techguy.org/6301111-post4.html
:GetDate
:: This subroutine will always display the same results,
:: for the date independent of "International" settings.
:: This batch file uses REG.EXE from the NT Resource Kit
:: (already installed with WinXP and Vista)
:: to read the "International" settings from the registry.
:: Date is returned as yyyymmdd in variable _fdate
:: Modified from SortDate Written by Rob van der Woude
:: http://www.robvanderwoude.com
IF NOT [%1]==[] SET _Date=%1
IF "%_Date%"=="!_Date:%2=!" GOTO:EOF
IF "%_Date%A" LSS "A" (SET _NumTok=1-3) ELSE (SET _NumTok=2-4)
IF %_iDate%==0 FOR /F "TOKENS=%_NumTok% DELIMS=%_sDate% " %%B IN ("%_Date%") DO SET _fdate=%%D%%B%%C
IF %_iDate%==1 FOR /F "TOKENS=%_NumTok% DELIMS=%_sDate% " %%B IN ("%_Date%") DO SET _fdate=%%D%%C%%B
IF %_iDate%==2 FOR /F "TOKENS=%_NumTok% DELIMS=%_sDate% " %%B IN ("%_Date%") DO SET _fdate=%%B%%C%%D
GOTO:EOF

:CreateScript
(
ECHO # List is automatically generated by dirMyD.bat file
ECHO # Directories ordered by Creation Time
ECHO # Created on %DATE%
ECHO.
FOR /F "TOKENS=2 DELIMS=*" %%A IN ('SORT %FILENAME%.tmp') DO (
    FIND "%%A" < %FILENAME%.MyD > NUL
    IF ERRORLEVEL 1 (
      ECHO SetVariable^(%VARIABLENAME%,^"%%A^"^)
      ECHO ^^!include %ZONEPATH%^^!
    )
))>%FILENAME%.MyD
DEL %FILENAME%.tmp
GOTO:EOF

:SearchDirectory
IF "%DRIVE:~1%"=="" SET DRIVE=%DRIVE%:
IF NOT EXIST %DRIVE% ECHO Drive not set for search to work^^! & GOTO:EOF
CD /D %DRIVE%
SET FOLDER=%~1
IF "%FOLDER:~0,2%"=="*\" SET FOLDER=%FOLDER:*\=%
ECHO Folder not set^^!
ECHO Searching "%FOLDER%" from drive %DRIVE%
FOR /F "TOKENS=* DELIMS=" %%A IN ('DIR /A:D/B/S^| FINDSTR /I /E /C:"%FOLDER%"') DO SET FOLDER=%%A& GOTO:EOF
GOTO:EOF

:Syntax
ECHO Syntax:
ECHO.
ECHO dirMyD ["full path"]
ECHO      Generates a list of directories in the path.
ECHO.
ECHO dirMyD /S [drive] ["partial path"]
ECHO      Searches for directory from the drive, and
ECHO      generates the list if directory found.
ECHO.
ECHO dirMyD scr
ECHO      Transforms temporal directory list to MyDefrag script.
ECHO.
ECHO Examples: dirMyD /S G: "*\Games"
ECHO           dirMyD "G:\Games"
ECHO           dirMyD scr
ECHO.
PAUSE
